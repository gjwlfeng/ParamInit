apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

configurations {
    // 声明一个自定义 configuration
    myImplementation
    // 使 implementation 继承于它
    implementation.extendsFrom(myImplementation)
}

dependencies {
    compileOnly group: 'com.google.android', name: 'android', version: '4.1.1.4'
    compileOnly group: 'io.github.gjwlfeng', name: 'ParamInit-lib-adapter', version: '1.0.0.1-SNAPSHOT'
    implementation group: 'io.github.gjwlfeng', name: 'ParamInit-lib-core', version: '1.0.4.1-SNAPSHOT'
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

version '1.0.4'
group = "io.github.gjwlfeng"
ext.artifactId = "ParamInit-lib-view-model"
archivesBaseName = artifactId

def isSnapshot = true

if (isSnapshot) {
    def fixVersion=1
    version(version + ".${fixVersion}-SNAPSHOT")
}

def myPomName = artifactId
def myPom = [
        name       : myPomName,
        artifactId : myPomName,
        description: "This is a gradle plug-in that records the project version log.",
        gitUrl     : "https://github.com/gjwlfeng/ParamInit",
        developer  : [
                id   : 'ZengFeng',
                name : 'ZengFeng',
                email: '903600017@qq.com',
        ],
        license    : [
                name: 'The Apache License, Version 2.0',
                url : 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        ]
]

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
//    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId myPom.artifactId
            artifact sourcesJar {
                classifier "sources"
            }

            pom {
                name = myPom.name
                description = myPom.description
                url = myPom.gitUrl
                licenses {
                    license {
                        name = myPom.license.name
                        url = myPom.license.url
                    }
                }

                developers {
                    developer {
                        id = myPom.developer.id
                        name = myPom.developer.name
                        email = myPom.developer.email
                    }
                }

                organization {
                    name = "zf"
                    url = myPom.gitUrl
                }
            }

            pom.withXml {
                NodeList nodeList = asNode().getByName("dependencies");
                def dependenciesNode
                if (nodeList.size() > 0) {
                    dependenciesNode = nodeList.get(0)
                } else {
                    dependenciesNode = asNode().appendNode('dependencies')
                }
                configurations.myImplementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dp.group)
                    dependencyNode.appendNode('artifactId', dp.name)
                    dependencyNode.appendNode('version', dp.version)                        // for exclusions
                    if (dp.excludeRules.size() > 0) {
                        def exclusions = dependencyNode.appendNode('exclusions')
                        dp.excludeRules.each { ExcludeRule ex ->
                            def exclusion = exclusions.appendNode('exclusion')
                            exclusion.appendNode('groupId', ex.group)
                            exclusion.appendNode('artifactId', ex.module)
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = new File('D:\\DevTools\\MavenRepo').toURI()
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name myPom.name
                packaging 'jar'
                // optionally artifactId can be defined here
                description myPom.description
                url myPom.gitUrl

                scm {
                    connection myPom.gitUrl
                    developerConnection myPom.gitUrl
                    url myPom.gitUrl
                }

                licenses {
                    license {
                        name myPom.license.name
                        url myPom.license.url
                    }
                }

                developers {
                    developer {
                        id myPom.developer.id
                        name myPom.developer.name
                        email myPom.developer.email
                    }
                }
            }
        }
    }
}

